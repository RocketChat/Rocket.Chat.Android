apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'io.fabric'

apply from: '../config/quality/quality.gradle'

def getCurrentCommitMessage() {
    def gitlog = 'git log origin/develop...HEAD --decorate'.execute().text
    if (gitlog.trim().length() > 0) return gitlog
    return 'git log -n 1 --decorate'.execute().text
}

def getKeyStore() {
    def path = System.getenv("KEYSTORE_PATH")
    return file((path == null) ? "../Rocket.jks" : path)
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    Properties properties = new Properties()
    properties.load(project.rootProject.file('app/fabric.properties').newDataInputStream())

    Properties localprop = new Properties()
    localprop.load(project.rootProject.file('local.properties').newDataInputStream())

    defaultConfig {
        applicationId "chat.rocket.android"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "0.0.0"
        manifestPlaceholders = [fabric_api_key: properties.getProperty("apiKey", System.getenv("FABRIC_API_KEY"))]
        ext.betaDistributionNotifications = false
        ext.betaDistributionReleaseNotes = getCurrentCommitMessage()
    }
    signingConfigs {
        release {
            storeFile getKeyStore()
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'

    compile 'com.github.clans:fab:1.6.2'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.parse.bolts:bolts-tasks:1.4.0'

    compile 'chat.rocket:android-ddp:0.0.2'

    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'

    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }

    compile 'org.nibor.autolink:autolink:0.5.0'
}
